package service
import domain.Book
import domain.Author
import domain.Genre
import value.Money

@Bean
class BookstoreService {
    
    fn getAllBooks() -> Book[] {
        return Book.allIdx.getAll(true)
    }
    
    fn findBookByIsbn(isbn: string) -> Book? {
        return Book.isbnIdx.getFirst(isbn)
    }
    
    fn findBooksByTitle(title: string) -> Book[] {
        return Book.titleIdx.getAll(title)
    }
    
    fn findBooksByAuthorName(authorName: string) -> Book[] {
        val author = Author.nameIdx.getFirst(authorName)
        if (author == null) {
            return Book.allIdx.getAll(false)  // empty result
        }
        return Book.authorIdx.getAll(author!!)
    }
    
    fn findBooksByGenre(genre: Genre) -> Book[] {
        return Book.genreIdx.getAll(genre)
    }
    
    fn addBook(title: string, authorName: string, isbn: string, 
              amount: double, currency: string, stock: int, 
              genre: Genre, description: string?, publishedYear: int?, rating: double) -> Book {
        val author = Author.nameIdx.getFirst(authorName)
        require(author != null, "Author not found: " + authorName)
        
        val existingBook = Book.isbnIdx.getFirst(isbn)
        require(existingBook == null, "Book with ISBN already exists: " + isbn)
        
        val price = Money(amount, currency, 0)  // no discount by default
        return Book(title, author!!, isbn, price, stock, genre, description, publishedYear, rating)
    }
    
    fn updateBookStock(isbn: string, quantity: int) -> bool {
        val book = Book.isbnIdx.getFirst(isbn)
        if (book == null) {
            return false
        }
        return book!!.updateStock(quantity)
    }
    
    fn updateBookRating(isbn: string, rating: double) -> bool {
        val book = Book.isbnIdx.getFirst(isbn)
        if (book == null) {
            return false
        }
        book!!.updateRating(rating)
        return true
    }
    
    fn getBookCount() -> int {
        return getAllBooks().length
    }
    
    fn getInStockBookCount() -> int {
        val books = getAllBooks()
        var count = 0
        for (book in books) {
            if (book.isInStock()) {
                count = count + 1
            }
        }
        return count
    }
}